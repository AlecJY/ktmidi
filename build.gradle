import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTargetWithTests

plugins {
    id 'com.android.library' version '4.1.3'
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.32'
    id 'org.jetbrains.dokka' version '1.4.30'
    id 'maven-publish'
}

group = 'dev.atsushieno'
version = '0.2.7'

repositories {
    google()
    jcenter()
    mavenCentral()
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
        testRuns["test"].executionTask.configure {
            useJUnit()
        }
    }
    android {
        publishLibraryVariantsGroupedByFlavor = true
        publishLibraryVariants("debug", "release")
    }
    js(LEGACY) {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                    webpackConfig.cssSupport.enabled = true
                }
            }
        }
    }
    iosX64('ios') {
        binaries {
            framework {
                baseName = 'library'
            }
        }
    }
    def hostOs = System.getProperty("os.name")
    def isMingwX64 = hostOs.startsWith("Windows")
    KotlinNativeTargetWithTests nativeTarget
    if (hostOs == "Mac OS X") nativeTarget = macosX64('native')
    else if (hostOs == "Linux") nativeTarget = linuxX64("native")
    else if (isMingwX64) nativeTarget = mingwX64("native")
    else throw new GradleException("Host OS is not supported in Kotlin/Native.")

    
    sourceSets {
        commonMain {
            dependencies {
                implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2')
                implementation('org.jetbrains.kotlinx:kotlinx-datetime:0.1.1')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {

        }
        androidMain {
            dependencies {
                implementation 'androidx.core:core-ktx:1.3.2'
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation 'junit:junit:4.13'
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        jsMain {

        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        nativeMain {

        }
        nativeTest {

        }
        iosMain {

        }
        iosTest {

        }
    }
}

android {
    compileSdkVersion 30
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/atsushieno/ktmidi")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
